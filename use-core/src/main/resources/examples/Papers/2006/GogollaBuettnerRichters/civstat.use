model CivilStatusWorld

enum CivilStatus {single, married, divorced, widowed}
enum Gender {female, male}

class Person

attributes
  name:String
  civstat:CivilStatus
  gender:Gender
  alive:Boolean

operations

birth(aName:String, aGender:Gender)
pre  freshUnlinkedPerson: name.isUndefined and civstat.isUndefined and
     gender.isUndefined and alive.isUndefined and
     wife.isUndefined and husband.isUndefined
post nameAssigned: name=aName -- equivalent to 'aName=name'
post civstatAssigned: civstat=CivilStatus::single
post genderAssigned: gender=aGender
post isAliveAssigned: alive=true -- equivalent to 'alive'

marry(aSpouse:Person)
pre  aSpouseDefined: aSpouse.isDefined
pre  isAlive: alive
pre  aSpouseAlive: aSpouse.alive
pre  isUnmarried: civstat<>CivilStatus::married
pre  aSpouseUnmarried: aSpouse.civstat<>CivilStatus::married
pre  differentGenders: gender<>aSpouse.gender
post isMarried: civstat=CivilStatus::married
post femaleHasMarriedHusband: gender=Gender::female implies
     husband=aSpouse and husband.civstat=CivilStatus::married
post maleHasMarriedWife: gender=Gender::male implies
     wife=aSpouse and wife.civstat=CivilStatus::married

divorce()
pre  isMarried: civstat=CivilStatus::married
pre  isAlive: alive
pre  husbandAlive: gender=Gender::female implies husband.alive
pre  wifeAlive: gender=Gender::male implies wife.alive
post isDivorced: civstat=CivilStatus::divorced
post husbandDivorced: gender=Gender::female implies
     husband.isUndefined and husband@pre.civstat=CivilStatus::divorced
post wifeDivorced: gender=Gender::male implies
     wife.isUndefined and wife@pre.civstat=CivilStatus::divorced

death()
pre  isAlive: alive
post notAlive: not(alive)
post husbandWidowed: gender=Gender::female and husband.isDefined implies
     husband@pre.wife.isUndefined and husband@pre.civstat=CivilStatus::widowed
post wifeWidowed: gender=Gender::male and wife.isDefined implies
     wife@pre.husband.isUndefined and wife@pre.civstat=CivilStatus::widowed

spouse():Person=if gender=Gender::female then husband else wife endif

constraints
  inv attributesDefined: name.isDefined and civstat.isDefined and
      gender.isDefined and alive.isDefined
  inv nameCapitalThenSmallLetters:
      let small:Set(String)=
        Set{'a','b','c','d','e','f','g','h','i','j','k','l','m',
            'n','o','p','q','r','s','t','u','v','w','x','y','z'} in
      let capital:Set(String)=
        Set{'A','B','C','D','E','F','G','H','I','J','K','L','M',
            'N','O','P','Q','R','S','T','U','V','W','X','Y','Z'} in
      capital->includes(name.substring(1,1)) and
      Set{2..name.size}->forAll(i |
        small->includes(name.substring(i,i)))
  inv nameIsUnique: Person.allInstances->forAll(self2|
      self<>self2 implies self.name<>self2.name)
  inv femaleHasNoWife: gender=Gender::female implies wife.isUndefined
  inv maleHasNoHusband: gender=Gender::male implies husband.isUndefined

end

association Marriage between
  Person [0..1] role wife
  Person [0..1] role husband
end
